plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.8'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'jacoco'
}

group = 'com.art'
version = '1.0.0'
description = 'TutorDesk is a management app designed for English tutors. It helps streamline scheduling, student tracking, lesson planning, and payment management in one convenient place.'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
}

bootJar {
    archiveFileName = 'tutordesk.jar'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.liquibase:liquibase-core'

    runtimeOnly 'com.h2database:h2:2.4.240'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    implementation 'org.mapstruct:mapstruct:1.5.5.Final'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.5.Final'
    annotationProcessor 'org.projectlombok:lombok-mapstruct-binding:0.2.0'

    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

jacoco {
    toolVersion = "0.8.14"
}

tasks.named('test') {
    useJUnitPlatform()
}

tasks.named('jacocoTestReport', JacocoReport) {
    dependsOn test
    reports {
        xml.required = true
        csv.required = false
        html.required = true
    }
    classDirectories.setFrom(
            sourceSets.main.output.asFileTree.filter {
                !it.name.contains('TutorDeskApplication') &&
                !it.name.contains('MapperImpl')
            }
    )
    executionData fileTree(layout.buildDirectory).include("jacoco/*.exec")
}

tasks.named('jacocoTestCoverageVerification', JacocoCoverageVerification) {
    dependsOn tasks.named('jacocoTestReport')

    violationRules {
        rule {
            element = 'CLASS'
            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.80
            }
            limit {
                counter = 'BRANCH'
                value = 'COVEREDRATIO'
                minimum = 0.75
            }
        }
    }
    classDirectories.setFrom(
            sourceSets.main.output.asFileTree.filter {
                !it.name.contains('TutorDeskApplication') &&
                !it.name.contains('MapperImpl')
            }
    )
    executionData fileTree(layout.buildDirectory).include("jacoco/*.exec")
}

check.dependsOn tasks.named('jacocoTestCoverageVerification')